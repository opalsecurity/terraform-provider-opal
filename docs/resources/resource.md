---
page_title: "opal_resource Resource - terraform-provider-opal"
subcategory: ""
description: |-
  An Opal Resource resource.
---

# opal_resource (Resource)

An Opal Resource resource.

## Example Usage

```terraform
resource "opal_resource" "sensitive_resource" {
  name = "Sensitive Resource"
  description = "A sensitive resource."
  resource_type = "CUSTOM"
  app_id = data.opal_app.my_custom_app.id
  admin_owner_id = opal_owner.security.id
  require_mfa_to_approve = true
  auto_approval = false

  reviewer_stage {
    reviewer {
      id = opal_owner.security.id
    }
  }
}
```

## Reviewer Configuration
Reviewer configuration can be managed via the `is_requestable`, `auto_approval`, and `reviewer_stage` attributes. See examples below:

```terraform
resource "opal_resource" "not_requestable" {
  // ...

  // If you want a resource to not be requestable, you can set `is_requestable` to false and omit the `reviewer_stage` attribute
  is_requestable = false
}

resource "opal_resource" "auto_approval" {
  // ...

  // If you want a resource to be auto-approved, you can set `auto_approval` to true and omit the `reviewer_stage` attribute
  auto_approval = false
}

resource "opal_resource" "basic_reviewer_config" {
  // ...

  // NOTE: operator = "AND" and require_manager_approval = false are the default if not explicitly set
  reviewer_stage {
    reviewer {
      id = opal_owner.security.id
    }
  }
}

resource "opal_resource" "or_reviewer_config" {
  // ...

  // Here the manager of the requesting user or the security owner would need to approve
  reviewer_stage {
    operator = "OR"
    require_manager_approval = true
    reviewer {
      id = opal_owner.security.id
    }
  }
}

resource "opal_resource" "complex_reviewer_config" {
  // ...

  // Here first the manager has to approve. Once the manager has approved, both the security owner and the data owner need to approve
  // NOTE: The ordering determines the ordering of the stages
  reviewer_stage {
    require_manager_approval = true
  }

  reviewer_stage {
    reviewer {
      id = opal_owner.security.id
    }

    reviewer {
      id = opal_owner.data.id
    }
  }
}
```

## Remote Resources
Remote resources can be managed using the `remote_info` attribute. See the examples below:

```terraform
resource "opal_resource" "aws_iam_role_example" {
  name = "AWS IAM role"
  description = "AWS IAM role created via terraform"
  resource_type = "AWS_IAM_ROLE"
  app_id = data.opal_app.aws.id
  admin_owner_id = opal_owner.security.id

  remote_info {
    aws_iam_role {
      # Note: This can reference your AWS terraform files
      arn = "arn:aws:iam::2582003"
    }
  }
}

resource "opal_resource" "aws_permission_set" {
  name                 = "AWS permission set"
  // ...

  remote_info {
    aws_permission_set {
      # Note: This can reference your AWS terraform files
      account_id = "234234234234"
      arn        = "arn:aws:sso:::permissionSet/ssoins-123123123abcdefg/ps-abc123abc123abcd"
    }
  }
}

resource "opal_resource" "okta_app_example" {
  name = "Okta app"
  // ...

  remote_info {
    okta_app {
      # Note: This can reference your Okta terraform files
      app_id = "0oa2aa0fcje6E2kXC5d7"
    }
  }
}

resource "opal_resource" "github_repo_example" {
  name = "GitHub repo"
  // ...

  remote_info {
    github_repo {
      # Note: This can reference your GitHub terraform files
      repo_name = "my-repo"
    }
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `admin_owner_id` (String) The admin owner ID for this resource.
- `app_id` (String) The ID of the app integration that provides the resource. You can get this value from the URL of the app in the Opal web app.
- `name` (String) The name of the resource.
- `resource_type` (String) The type of the resource, i.e. AWS_EC2_INSTANCE.

### Optional

- `auto_approval` (Boolean) Automatically approve all requests for this resource without review.
- `description` (String) The description of the resource.
- `is_requestable` (Boolean) Allow users to create an access request for this resource. By default, any resource is requestable.
- `max_duration` (Number) The maximum duration for which this resource can be requested (in minutes).
- `recommended_duration` (Number) The recommended duration for which the resource should be requested (in minutes). Will be the default value in a request. Use -1 to set to indefinite.
- `remote_info` (Block List, Max: 1) Remote info that is required for the creation of remote resources. (see [below for nested schema](#nestedblock--remote_info))
- `request_template_id` (String) The ID of a request template for this resource. You can get this ID from the URL in the Opal web app.
- `require_mfa_to_approve` (Boolean) Require that reviewers MFA to approve requests for this resource.
- `require_mfa_to_connect` (Boolean) Require that users MFA to connect to this resource. Only applicable for resources where a session can be started from Opal (i.e. AWS RDS database)
- `require_mfa_to_request` (Boolean) Require that users MFA to request this resource.
- `require_support_ticket` (Boolean) Require that requesters attach a support ticket to requests for this resource.
- `reviewer_stage` (Block List) A reviewer stage for this resource. You are allowed to provide up to 3. (see [below for nested schema](#nestedblock--reviewer_stage))
- `visibility` (String) The visibility level of the resource, i.e. LIMITED or GLOBAL.
- `visibility_group` (Block List) The groups that can see this resource when visibility is limited. If not specified, only admins and users with direct access can see this resource when visibility is set to LIMITED. (see [below for nested schema](#nestedblock--visibility_group))

### Read-Only

- `id` (String) The ID of the resource.

<a id="nestedblock--remote_info"></a>
### Nested Schema for `remote_info`

Optional:

- `aws_account` (Block List, Max: 1) The remote_info for an AWS account. (see [below for nested schema](#nestedblock--remote_info--aws_account))
- `aws_ec2_instance` (Block List, Max: 1) The remote_info for an AWS EC2 instance. (see [below for nested schema](#nestedblock--remote_info--aws_ec2_instance))
- `aws_eks_cluster` (Block List, Max: 1) The remote_info for an AWS EKS cluster. (see [below for nested schema](#nestedblock--remote_info--aws_eks_cluster))
- `aws_iam_role` (Block List, Max: 1) The remote_info for an AWS IAM role. (see [below for nested schema](#nestedblock--remote_info--aws_iam_role))
- `aws_permission_set` (Block List, Max: 1) The remote_info for an AWS permission set. (see [below for nested schema](#nestedblock--remote_info--aws_permission_set))
- `aws_rds_instance` (Block List, Max: 1) The remote_info for an AWS RDS instance. (see [below for nested schema](#nestedblock--remote_info--aws_rds_instance))
- `github_repo` (Block List, Max: 1) The remote_info for a Github repo. (see [below for nested schema](#nestedblock--remote_info--github_repo))
- `gitlab_project` (Block List, Max: 1) The remote_info for a Gitlab project. (see [below for nested schema](#nestedblock--remote_info--gitlab_project))
- `okta_app` (Block List, Max: 1) The remote_info for an Okta app. (see [below for nested schema](#nestedblock--remote_info--okta_app))
- `okta_custom_role` (Block List, Max: 1) The remote_info for an Okta custom role. (see [below for nested schema](#nestedblock--remote_info--okta_custom_role))
- `okta_standard_role` (Block List, Max: 1) The remote_info for an Okta standard role. (see [below for nested schema](#nestedblock--remote_info--okta_standard_role))
- `teleport_role` (Block List, Max: 1) The remote_info for a Teleport role. (see [below for nested schema](#nestedblock--remote_info--teleport_role))

<a id="nestedblock--remote_info--aws_account"></a>
### Nested Schema for `remote_info.aws_account`

Required:

- `account_id` (String) The ID of the AWS account.


<a id="nestedblock--remote_info--aws_ec2_instance"></a>
### Nested Schema for `remote_info.aws_ec2_instance`

Required:

- `instance_id` (String) The instanceId of the EC2 instance.
- `region` (String) The region of the EC2 instance.

Optional:

- `account_id` (String) The ID of the AWS account.


<a id="nestedblock--remote_info--aws_eks_cluster"></a>
### Nested Schema for `remote_info.aws_eks_cluster`

Required:

- `arn` (String) The ARN of the EKS cluster.

Optional:

- `account_id` (String) The ID of the AWS account.


<a id="nestedblock--remote_info--aws_iam_role"></a>
### Nested Schema for `remote_info.aws_iam_role`

Required:

- `arn` (String) The ARN of the IAM role.

Optional:

- `account_id` (String) The ID of the AWS account.


<a id="nestedblock--remote_info--aws_permission_set"></a>
### Nested Schema for `remote_info.aws_permission_set`

Required:

- `account_id` (String) The ID of the AWS account.
- `arn` (String) The ARN of the permission set.


<a id="nestedblock--remote_info--aws_rds_instance"></a>
### Nested Schema for `remote_info.aws_rds_instance`

Required:

- `instance_id` (String) The instanceId of the RDS instance.
- `region` (String) The region of the RDS instance.
- `resource_id` (String) The resourceId of the RDS instance.

Optional:

- `account_id` (String) The ID of the AWS account.


<a id="nestedblock--remote_info--github_repo"></a>
### Nested Schema for `remote_info.github_repo`

Required:

- `repo_name` (String) The name of the repository.


<a id="nestedblock--remote_info--gitlab_project"></a>
### Nested Schema for `remote_info.gitlab_project`

Required:

- `project_id` (String) The id of the project.


<a id="nestedblock--remote_info--okta_app"></a>
### Nested Schema for `remote_info.okta_app`

Required:

- `app_id` (String) The id of the app.


<a id="nestedblock--remote_info--okta_custom_role"></a>
### Nested Schema for `remote_info.okta_custom_role`

Required:

- `role_id` (String) The id of the role.


<a id="nestedblock--remote_info--okta_standard_role"></a>
### Nested Schema for `remote_info.okta_standard_role`

Required:

- `role_type` (String) The type of the role.


<a id="nestedblock--remote_info--teleport_role"></a>
### Nested Schema for `remote_info.teleport_role`

Required:

- `role_name` (String) The name of the role.



<a id="nestedblock--reviewer_stage"></a>
### Nested Schema for `reviewer_stage`

Optional:

- `operator` (String) The operator of the stage. Operator is either "AND" or "OR".
- `require_manager_approval` (Boolean) Whether this reviewer stage should require manager approval.
- `reviewer` (Block Set) A reviewer for this stage. (see [below for nested schema](#nestedblock--reviewer_stage--reviewer))

<a id="nestedblock--reviewer_stage--reviewer"></a>
### Nested Schema for `reviewer_stage.reviewer`

Required:

- `id` (String) The ID of the owner.



<a id="nestedblock--visibility_group"></a>
### Nested Schema for `visibility_group`

Required:

- `id` (String) The ID of the group that can see this resource.

Please [file a ticket](https://github.com/opalsecurity/terraform-provider-opal/issues) to discuss use cases that are not yet supported in the provider.
